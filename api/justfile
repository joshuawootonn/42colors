# Show available API commands
default:
  @just --list

# Setup and dependencies
install:
  mix deps.get

# Testing
test:
  export $(xargs <.env.test) && mix clean && mix compile && mix test

test-watch:
  export $(xargs <.env.test) && mix clean && mix compile && mix test.watch

# Database

create-migration name:
  mix ecto.gen.migration {{name}}

migrate:
  export $(xargs <.env.dev) && mix ecto.migrate

migrate-test:
  export $(xargs <.env.test) && mix ecto.migrate

# Reset database (drops, creates, migrates, seeds)
reset-dev-db:
  export $(xargs <.env.dev) && mix ecto.reset

reset-test-db:
  export $(xargs <.env.test) && mix ecto.reset

# Force reset by dropping connections first (use when reset-dev-db fails)
reset-force:
  #!/usr/bin/env bash
  export $(xargs <.env.dev)
  echo "ðŸ§¹ Cleaning and compiling dev environment..."
  mix clean
  mix compile
  echo "ðŸ”Œ Terminating active database connections..."
  mix ecto.drop --force
  echo "ðŸ—„ï¸  Creating fresh database..."
  mix ecto.create
  echo "â¬†ï¸  Running migrations..."
  mix ecto.migrate
  echo "ðŸŒ± Seeding database..."
  mix run priv/repo/seeds.exs
  echo "âœ… Database reset complete!"

# Force reset test database
reset-test-force:
  #!/usr/bin/env bash
  export $(xargs <.env.test)
  echo "ðŸ§¹ Cleaning and compiling test environment..."
  mix clean
  mix compile
  echo "ðŸ”Œ Terminating active test database connections..."
  mix ecto.drop --force
  echo "ðŸ—„ï¸  Creating fresh test database..."
  mix ecto.create
  echo "â¬†ï¸  Running test migrations..."
  mix ecto.migrate
  echo "âœ… Test database reset complete!"

# Development server
dev:
  kill $(lsof -t -i:4000) || true && orb start && docker compose --project-directory ../docker up -d && export $(xargs <.env.dev) && mix phx.server

# Vote settlement
settle-votes:
  export $(xargs <.env.dev) && mix settle_votes

settle-votes-date date:
  export $(xargs <.env.dev) && mix settle_votes {{date}}

# Oban job monitoring
oban-status:
  mix oban_status

oban-failed:
  mix oban_status --failed

oban-scheduled:
  mix oban_status --scheduled

# Code quality
format:
  mix format

format-check:
  mix format --check-formatted

# Utilities
clean:
  mix clean

compile:
  export $(xargs <.env.dev) && mix compile

# Development dashboards
dashboards:
  @echo "ðŸ“§ Email dashboard: http://localhost:4000/dev/mailbox"
  @echo "ðŸ“Š LiveDashboard: http://localhost:4000/admin/dashboard"
  @echo "ðŸ“ˆ Prometheus:     http://localhost:9090 (localhost only)"
  @echo "ðŸ“‰ Grafana:        http://localhost:9080 (ADMIN_USERNAME/ADMIN_PASSWORD)"
  @echo "ðŸ“Š Metrics:        http://localhost:4000/metrics (requires ADMIN auth)"

# Export PromEx dashboards to Grafana provisioning directory (local + production)
export-dashboards:
  #!/usr/bin/env bash
  export $(xargs <.env.dev)
  LOCAL_DIR="../docker/grafana/provisioning/dashboards/json"
  PROD_DIR="config/grafana/provisioning/dashboards/json"
  mkdir -p $LOCAL_DIR $PROD_DIR
  echo "ðŸ“Š Exporting PromEx dashboards..."
  mix prom_ex.dashboard.export -d application.json -m Api.PromEx -s 2>/dev/null | sed -n '/^{/,$p' > $LOCAL_DIR/application.json
  mix prom_ex.dashboard.export -d beam.json -m Api.PromEx -s 2>/dev/null | sed -n '/^{/,$p' > $LOCAL_DIR/beam.json
  mix prom_ex.dashboard.export -d phoenix.json -m Api.PromEx -s 2>/dev/null | sed -n '/^{/,$p' > $LOCAL_DIR/phoenix.json
  mix prom_ex.dashboard.export -d ecto.json -m Api.PromEx -s 2>/dev/null | sed -n '/^{/,$p' > $LOCAL_DIR/ecto.json
  mix prom_ex.dashboard.export -d oban.json -m Api.PromEx -s 2>/dev/null | sed -n '/^{/,$p' > $LOCAL_DIR/oban.json
  # Copy to production config directory
  cp $LOCAL_DIR/*.json $PROD_DIR/
  echo "âœ… Dashboards exported to $LOCAL_DIR and $PROD_DIR"

deploy: 
  export $(xargs < .env.prod) && kamal deploy 

logs:
  kamal app logs
